// ebnf for prolog (kind of)

// loosely based on
// https://en.wikipedia.org/wiki/Prolog_syntax_and_semantics

// This will work for simple stuff, the feature set
// you need for domain modeling and building most expert
// systems. However, the structure of parse tree is
// actually pretty different from most prolog interpreters.
// So, e.g. this will probably not run other peoples'
// libraries.

// some lark inbuilt regexes, should be obvious
%import common.WS
%import common.SIGNED_NUMBER
%import common.ESCAPED_STRING

// smart, kills newlines etc.
%ignore WS

// there are a number of things the user might give us
start: (logical | rule | program)

// these represent different parsing options for the
// guys outside
program: ((declaration | rule | logical) ".")*

// functor declarations
// note that this ignores the dynamic key word because
// everything is dynamic in my implementation
declaration: "dynamic"? functor "/" arity
arity: /[0-9]+/

// the primary prolog datatype
?logical:
    | neg
    | keyword
    | var
    | term
    | disj
    | conj
    | list                // ↓ aliases ↓
//    | SIGNED_NUMBER         -> number
//    | ESCAPED_STRING        -> string     // add these back when there is parser handling

neg:
    | "\+" term
    | "not(" term ")"

// some things that should not be interpreted as atoms
// true, false, and fail are atoms in many prologs
// but I translate them to distinct python objects
keyword:
    | "!"                   -> cuts
    | "true"                -> true
    | "false"               -> fail
    | "fail"                -> fail

// a var is an uppercase letter of underscore
// followed by text (no spaces)
var: /[A-Z_]\w*/

// a term is a (single quoted?) string optionally followed by
// a list of arguments in parenthesis
?term:
    | compound_term
    | var

compound_term: functor ("(" term ("," term)* ")")?

?functor:
    | /([a-z])\w+/
    | "'" /[a-z][\w ]*/ "'"

// I'm implementing lists as arrays rather than linked lists
// like in normal prolog, this because the interpreter is in
// python and python doesn't like linked lists
list:
    | "[" term ("," term)* "]"
    | "[]"

rule: head  ":-" body
head: term
body: logical

// note the parenthesis mean "and" takes precedence over "or"
disj: (term | conj) (";" (term | conj))+
conj: (term | "(" disj ")") ("," (term | "(" disj ")"))+
